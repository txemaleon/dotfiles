# Path
export PATH=$HOME/.local/bin:/usr/local/bin:$PATH

# History configuration
HISTSIZE=50000
SAVEHIST=50000
HISTFILE=~/.zsh_history
setopt HIST_IGNORE_ALL_DUPS  # Remove older duplicates
setopt HIST_IGNORE_SPACE     # Ignore commands starting with space
setopt HIST_NO_FUNCTIONS     # Don't save functions
setopt HIST_REDUCE_BLANKS    # Remove superfluous blanks
setopt SHARE_HISTORY         # Share history between sessions

# Line editing
bindkey -e  # Emacs mode (Ctrl+A/E/U/W work in terminal)

# Up/Down search history based on current input
autoload -Uz up-line-or-beginning-search down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "^[[A" up-line-or-beginning-search    # Up arrow
bindkey "^[[B" down-line-or-beginning-search  # Down arrow
bindkey "^[^?" backward-kill-word             # Alt+Backspace: delete word
setopt AUTO_CD               # cd by typing directory name
setopt INTERACTIVE_COMMENTS  # Allow comments in interactive shell

# Filter commands from history (uses HISTORY_IGNORE from exports)
zshaddhistory() {
  emulate -L zsh
  [[ $1 != ${~HISTORY_IGNORE} ]]
}

# Completion settings
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Za-z}'  # Case insensitive
zstyle ':completion:*' menu select                       # Menu selection
zstyle ':completion:*' list-colors "${(s.:.)LS_COLORS}" # Colored completions
autoload -Uz compinit && compinit

# Zinit plugin manager
ZINIT_HOME="${XDG_DATA_HOME:-$HOME/.local/share}/zinit/zinit.git"
if [[ ! -d $ZINIT_HOME ]]; then
  mkdir -p "$(dirname $ZINIT_HOME)"
  git clone https://github.com/zdharma-continuum/zinit.git "$ZINIT_HOME"
fi
source "${ZINIT_HOME}/zinit.zsh"

# Plugins
zinit light zsh-users/zsh-autosuggestions
zinit light zsh-users/zsh-syntax-highlighting
zinit light zsh-users/zsh-completions
zinit light Aloxaf/fzf-tab

# Tool integrations (cached for faster startup, regenerate with: regen-shell-cache)
__cache_dir="${XDG_CACHE_HOME:-$HOME/.cache}/zsh"
mkdir -p "$__cache_dir"

__load_cached() {
  local name="$1" cmd="$2" args="$3"
  local cache="$__cache_dir/$name.zsh"
  if command -v "$cmd" &>/dev/null; then
    [[ ! -f "$cache" || "$cache" -ot "$(command -v "$cmd")" ]] && "$cmd" $args > "$cache"
    source "$cache"
  fi
}

__load_cached fzf fzf "--zsh"
__load_cached zoxide zoxide "init zsh"
__load_cached mise mise "activate zsh"
__load_cached bun bun "completions"

regen-shell-cache() {
  rm -rf "$__cache_dir"
  echo "Shell cache cleared. Restart your shell to regenerate."
}

unset __cache_dir

# User configuration
source $HOME/.profile

# Prompt (matches edvardm theme)
autoload -Uz vcs_info
zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:git:*' stagedstr '%F{yellow}✗%f'
zstyle ':vcs_info:git:*' unstagedstr '%F{yellow}✗%f'
zstyle ':vcs_info:git:*' formats ' %F{blue}git:(%F{red}%b%F{blue})%f%u%c'
zstyle ':vcs_info:git:*' actionformats ' %F{blue}git:(%F{red}%b|%a%F{blue})%f'
precmd() { vcs_info }
setopt PROMPT_SUBST
PROMPT='%B%F{red}➜%f %F{white}%c%f%b${vcs_info_msg_0_} '
