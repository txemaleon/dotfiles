#!/usr/bin/env zsh

# Git flow
function nfeat {
	local slug=$(__getNewBranchName $@)
	[[ -z $slug ]] && echo "Error: You must supply a branch name" && return
	git checkout -b feature/$slug $(git mb)
}

function nfix {
	local slug=$(__getNewBranchName $@)
	[[ -z $slug ]] && echo "Error: You must supply a branch name" && return
	git checkout -b fix/$slug $(git mb)
}

function nchore {
	local slug=$(__getNewBranchName $@)
	[[ -z $slug ]] && echo "Error: You must supply a branch name" && return
	git checkout -b chore/$slug $(git mb)
}

function repo_has_remote {
	[[ -z $(git remote -v 2>/dev/null) ]] && return 1
	return 0
}

# Merging
function qm {
	[[ -z $1 ]] && echo "Error: You must supply a branch name" && return
	local CURRENT_BRANCH=$(git current-branch)
	git switch $1 || {
		echo "Error switching to $1"
		return 1
	}
	if repo_has_remote; then
		git pull || {
			echo "Error pulling $1"
			git switch $CURRENT_BRANCH >/dev/null 2>&1
			return 1
		}
	fi
	git merge $CURRENT_BRANCH -m "Merge branch \`$CURRENT_BRANCH\` into \`$1\`" --strategy-option theirs || {
		echo "Error merging $(g bn) in $1"
		git merge --abort >/dev/null 2>&1
		git switch $CURRENT_BRANCH >/dev/null 2>&1
		return 1
	}
	if repo_has_remote; then
		git push || {
			echo "Error pushing $1"
			git switch $CURRENT_BRANCH >/dev/null 2>&1
			return 1
		}
	fi
	git switch $CURRENT_BRANCH || return 1
}

alias qmm='qm $(git mb)'

function qmd {
	[[ -z $1 ]] && echo "Error: You must supply a branch name" && return 1

	local CURRENT_BRANCH=$(git current-branch)

	qm $1 || {
		echo "Merge failed. Not deleting $CURRENT_BRANCH"
		return 1
	}
	git switch $1 || {
		echo "Error switching back to $1. Not deleting $CURRENT_BRANCH"
		return 1
	}
	gdb $CURRENT_BRANCH
}

alias qmmd='qmd $(git mb)'

function qms {
	[[ -z $1 ]] && echo "Error: You must supply a branch name" && return 1

	local CURRENT_BRANCH=$(git current-branch)
	git switch $1 || {
		echo "Error switching to $1"
		return 1
	}
	if repo_has_remote; then
		git pull || {
			echo "Error pulling $1"
			git switch $CURRENT_BRANCH >/dev/null 2>&1
			return 1
		}
	fi
	git merge --squash -X theirs $CURRENT_BRANCH || {
		echo "Error squash-merging $(g bn) in $1"
		git merge --abort >/dev/null 2>&1
		git switch $CURRENT_BRANCH >/dev/null 2>&1
		return 1
	}
	git commit -m "Squash merge branch \`$CURRENT_BRANCH\` into \`$1\`" || {
		echo "Error committing squash merge in $1"
		git switch $CURRENT_BRANCH >/dev/null 2>&1
		return 1
	}
	if repo_has_remote; then
		git push || {
			echo "Error pushing $1"
			git switch $CURRENT_BRANCH >/dev/null 2>&1
			return 1
		}
	fi
	git switch $CURRENT_BRANCH || return 1
}

function qmsd {
	[[ -z $1 ]] && echo "Error: You must supply a branch name" && return 1

	local CURRENT_BRANCH=$(git current-branch)
	qms $1 || {
		echo "Squash merge failed. Not deleting $CURRENT_BRANCH"
		return 1
	}
	git switch $1 || {
		echo "Error switching back to $1. Not deleting $CURRENT_BRANCH"
		return 1
	}
	gdb $CURRENT_BRANCH
}

alias qmmsd='qmsd $(git mb)'

# Deleting
function gdb {
	[[ -z $1 ]] && echo "Error: You must supply a branch name" && return

	git branch -D $1 && git push origin --delete $1 2>/dev/null
}

function gqdb {
	local CURRENT_BRANCH=$(git current-branch)

	git switch $(git mb) && gdb $CURRENT_BRANCH
}

# Check if merged
git-is-merged() {
	local merge_destination_branch=$1
	local merge_source_branch=$2

	if [[ -z $(git branch -l $merge_destination_branch 2>/dev/null) ]]; then
		return 1
	fi

	local merge_base=$(git merge-base $merge_destination_branch $merge_source_branch)
	local merge_source_current_commit=$(git rev-parse $merge_source_branch)
	if [[ $merge_base == $merge_source_current_commit ]]; then
		echo $merge_source_branch is merged into $merge_destination_branch
		return 0
	else
		echo "$merge_source_branch is \033[1;31mnot\033[0m merged into $merge_destination_branch"
		return 1
	fi
}

alias isindev='git-is-merged develop $(git current-branch)'
alias isinstg='git-is-merged staging $(git current-branch)'
alias isinsnd='git-is-merged sandbox $(git current-branch)'
alias isinm='git-is-merged $(git mb) $(git current-branch)'
alias ism='isindev; isinstg; isinsnd; isinm'

# Getting branch info
function __getNewBranchName {
	if [[ -z $1 ]]; then
		read "new_branch_name?Enter your new branch name: "
	else
		new_branch_name=$@
	fi

	echo $new_branch_name | sed -E 's/[~^]+//g' | sed -E 's/[^a-zA-Z0-9\/]+/-/g' | sed -E 's/^-+|-+$//g' | sed -E 's/^feature\///g' | sed -E 's/^bugfix\///g' | sed -E 's/^fix\///g' | tr A-Z a-z
}
